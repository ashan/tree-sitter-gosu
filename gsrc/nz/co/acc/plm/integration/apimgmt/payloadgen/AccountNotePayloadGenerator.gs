package nz.co.acc.plm.integration.apimgmt.payloadgen

uses com.fasterxml.jackson.core.JsonGenerator
uses com.fasterxml.jackson.databind.SerializerProvider
uses com.fasterxml.jackson.databind.ser.PropertyWriter
uses com.fasterxml.jackson.databind.ser.impl.SimpleBeanPropertyFilter
uses nz.co.acc.common.GenericConstants
uses nz.co.acc.common.integration.apimgmt.json.JSONNote
uses nz.co.acc.common.integration.apimgmt.payloadgen.AbstractPayloadGenerator
uses nz.co.acc.common.integration.apimgmt.payloadgen.GenFlags

uses java.text.SimpleDateFormat

/**
 * Payload generator for the Note entity.
 */
class AccountNotePayloadGenerator extends AbstractPayloadGenerator<JSONNote, Note> {

  construct(note : Note) {
    super(note)
  }

  override function generate(flags : GenFlags[]) : JSONNote {
    if (entity == null) {
      return null
    }

    var pNote = new JSONNote()
    var sdf = new SimpleDateFormat(GenericConstants.ISO8601_TIMESTAMP_PATTERN)

    pNote.LinkID = entity.getLinkID()
    pNote.Topic = entity.Topic?.Code
    pNote.SubTopic = entity.SubTopic_ACC?.Code
    pNote.Subject = entity.Subject.NotBlank ? entity.Subject : "Guidewire Notes"
    pNote.SecurityType = entity.SecurityType?.Code
    pNote.Text = entity.Body
    pNote.UpdateTime = sdf.format(entity.UpdateTime)

    if (not flags.contains(GenFlags.ROOT_ONLY)) {
      var noteAuthor = entity.Author ?: entity.CreateUser
      pNote.Author = new UserPayloadGenerator(noteAuthor).generate({})
    }

    return pNote
  }

  /**
   * Filter to apply when serialising the payload generated by this generator.
   * <p>
   * Note: This filter got obsoleted by a change to the requirements but leaving the code as
   * an example for creating a filter for a json payload.
   *
   * @return json property filter
   */
  function getFilter() : SimpleBeanPropertyFilter {
    return new SimpleBeanPropertyFilter() {
      override function serializeAsField(pojo : Object, jgen : JsonGenerator, provider : SerializerProvider, writer : PropertyWriter) {
        /*if (include(writer)) {
          var note = pojo as JSONNote
          if (writer.Name == "CreateTime" && not note.IsNew) {
            return
          }
          super.serializeAsField(pojo, jgen, provider, writer)
        }*/
      }
    }
  }

}